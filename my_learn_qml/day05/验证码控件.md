# 验证码控件

今天学习Canvas画布,看到了旋转,突然回想到之前用QtPaint也写了一个验证码的QtWidget就想着拿qml来复刻一个.

思路挺简单的,需要一个生成验证码的函数并保存验证码(我这里就简单的生成一个验证码),然后直接一个一个写入字符并旋转,之后加入干扰线和干扰点.后续需要其他特点可以自己添加.

```
//AuthCodeItem.qml
import QtQuick
import QtQuick.Controls
Rectangle{
    id:control;
    width:200;
    height:40;
    color:Qt.rgba(Math.random(),Math.random(),Math.random(),0.6);
    property string code: "";
    property int codeFontSize: control.height;
    property int codeSize: 4;
    //数字
    Canvas{
        id:canvasControl;
        contextType:"2d";
        width:parent.width;
        height:parent.height;
        onPaint: {
            context.beginPath();
             context.clearRect(0, 0, width, height)
            context.font=control.codeFontSize+"px  sans-serif";
            var XYmargin =control.height/5;
            for(var i = 0; i < codeSize; i++){
                context.save(); // 保存当前状态
                console.log(code.charAt(i));
                // 计算字符位置（每个字符占1/4宽度，居中）
                var charWidth = width / 4 - 10;
                var x = charWidth * i + charWidth / 2+XYmargin;
                var y = height / 2 + XYmargin;
                // 移动到字符中心点
                context.translate(x, y);
                context.fillStyle = Qt.rgba(Math.random(),Math.random(),Math.random(),1) ;
                // 设置随机旋转角度（-45到45度之间）
                var angle = (Math.random() * 90 - 45) * Math.PI / 180;
                context.rotate(angle);
                context.fillText(code.charAt(i),0,0);
                context.restore();
            }
            // 添加干扰线
            for(var j = 0; j < 10; j++) {
                context.beginPath();
                context.strokeStyle = Qt.rgba(Math.random(), Math.random(), Math.random(), 0.5);
                context.moveTo(Math.random() * width, Math.random() * height);
                context.lineTo(Math.random() * width, Math.random() * height);
                context.stroke();
            }

            // 添加干扰点
            for(var k = 0; k < 50; k++) {
                context.beginPath();
                context.arc(
                            Math.random() * width,
                            Math.random() * height,
                            1 + Math.random(), // 随机半径
                            0,
                            Math.PI * 2
                            );
                context.fillStyle = Qt.rgba(Math.random(), Math.random(), Math.random(), 0.7);
                context.fill();
            }
            context.restore();
        }
        // 初始化时生成验证码
        Component.onCompleted: {
            randomCode();
        }
    }
    MouseArea{
        anchors.fill: parent;
        onPressed: {
            randomCode();
        }
    }
    //生成随机数 -- 
    function randomCode(){
        //
        var chars = "smnPQRpqrBM456VWHIdefg78lctuSTCDJK90ovwXYZ1EFGxyzAab23jkLUhiNO";
        var code = "";
        // 生成4位随机字符
        for (var i = 0; i < codeSize; i++) {
            var index = Math.floor(Math.random()*1237)% chars.length;    //1237 随便取的
            code += chars.charAt(index);
        }
        control.code = code;
        console.log(code);
        canvasControl.requestPaint() ;// 重绘画布
        return code;
    }
}
```

